import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "6.3.1.5724"

    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

ext {
    jacocoVersion = '0.8.13'
    checkstyleVersion = "11.1.0"
    pmdVersion = '7.17.0'
}

group = 'org.theoriok'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}
tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
        mavenBom 'com.vaadin:vaadin-bom:24.3.12'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'com.github.dpaukov:combinatoricslib3:3.4.0'
    implementation 'org.jgrapht:jgrapht-core:1.5.2'
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
    jvmArgs("--enable-preview")
}

pmd {
    ruleSets = []
    ignoreFailures = false
    toolVersion = pmdVersion
}

pmdMain {
    ruleSetFiles = files("${project.rootDir}/config/pmd/pmd.xml")
}

pmdTest {
    ruleSetFiles = files("${project.rootDir}/config/pmd/testPmd.xml")
}

checkstyle {
    toolVersion = checkstyleVersion
    ignoreFailures = false
    maxErrors = 0
    maxWarnings = 0
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}


jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "theoriok_adventofcode"
        property "sonar.organization", "theorioksoft"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.rootDir}/build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.tests", "src/test"
    }
}
